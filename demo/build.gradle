apply plugin: "kotlin"

repositories {
  google()
  mavenCentral()
  jcenter()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

// Lint.

apply plugin: "com.android.lint"

lintOptions {
  abortOnError true
  warningsAsErrors true
  // baseline project.file("baseline-lint.xml")
}

dependencies {
  // Unfortunately this does not quite work for Kotlin projects - https://issuetracker.google.com/issues/112526243
  lintChecks project(":custom-android-lint-rules")
}

// Detekt.

configurations {
  detekt
}

dependencies {
  detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
  detekt project(":custom-detekt-rules")
}

def output = new File(project.buildDir, "reports/detekt/")

task detekt(type: JavaExec, group: "verification", description: "Runs detekt.") {
  def configFile = file("code_quality_tools/detekt.yml")

  main = "io.gitlab.arturbosch.detekt.cli.Main"
  classpath = project.configurations.detekt
  args = [
    "--config", configFile,
    "--input", project.file("."),
    "--report", "txt:$output/plain.txt,xml:$output/checkstyle.xml,html:$output/report.html"
  ]

  // To make this Gradle task incremental - https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
  inputs.files(project.fileTree(dir: "src", include: "**/*.kt"), configFile)
  outputs.dir(output.toString())
}

// We don't want them to be checked too since this is a demo and it's supposed to fail here.
// check.dependsOn "detekt"

// ktlint.

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:$ktlintVersion"
  ktlint project(":custom-ktlint-rules")
}

def outputDir = "${project.buildDir}/reports/ktlint/"

task ktlint(type: JavaExec, group: "verification", description: "Runs ktlint.") {
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args = [
    "--reporter=plain",
    "--reporter=checkstyle,output=${outputDir}ktlint-checkstyle-report.xml",
    "src/**/*.kt"
  ]

  // To make this Gradle task incremental - https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
  inputs.files(
    fileTree(dir: "src", include: "**/*.kt"),
    fileTree(dir: ".", include: "**/.editorconfig")
  )
  outputs.dir(outputDir)
}

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Runs ktlint and autoformats your code."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args = [
    "-F",
    "src/**/*.kt"
  ]

  // To make this Gradle task incremental - https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
  inputs.files(
    fileTree(dir: "src", include: "**/*.kt"),
    fileTree(dir: ".", include: "**/.editorconfig")
  )
  outputs.upToDateWhen { true }
}

// We don't want them to be checked too since this is a demo and it's supposed to fail here.
// check.dependsOn "ktlint"
