apply plugin: "kotlin"

repositories {
  google()
  mavenCentral()
  jcenter()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

// Lint.

apply plugin: "com.android.lint"

lintOptions {
  abortOnError true
  warningsAsErrors true
}

// Detekt.

configurations {
  detekt
}

dependencies {
  //noinspection AndroindLintGradleDependency https://issuetracker.google.com/issues/109956042
  detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
  detekt project(":custom-detekt-rules")
}

def output = new File(project.buildDir, "reports/detekt/")

task detekt(type: JavaExec, group: "verification", description: "Runs detekt.") {
  def configFile = file("code_quality_tools/detekt.yml")
  inputs.files(project.fileTree(dir: "src", include: "**/*.kt"), configFile)
  outputs.dir(output.toString())
  main = "io.gitlab.arturbosch.detekt.cli.Main"
  classpath = project.configurations.detekt
  args = [ "--config", configFile, "--input", project.file("."), "--report", "plain:$output/plain.txt,xml:$output/checkstyle.xml,html:$output/report.html" ]
}

// We don't want them to be checked too since this is a demo and it's supposed to fail here.
// check.dependsOn "detekt"

// ktlint.

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:$ktlintVersion"
  ktlint project(":custom-ktlint-rules")
}

def outputDir = "${project.buildDir}/reports/ktlint/"

task ktlint(type: JavaExec, group: "verification", description: "Runs ktlint.") {
  inputs.files(fileTree(dir: "src", include: "**/*.kt"),
      fileTree(dir: ".", include: "**/.editorconfig"))
  outputs.dir(outputDir)
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args = [ "--reporter=plain",
    "--reporter=checkstyle,output=${outputDir}ktlint-checkstyle-report.xml",
    "src/**/*.kt" ]
}

task ktlintFormat(type: JavaExec, group: "formatting") {
  inputs.files(
    fileTree(dir: "src", include: "**/*.kt"),
    fileTree(dir: ".", include: "**/.editorconfig")
  )
  outputs.upToDateWhen { true }
  description = "Runs ktlint and autoformats your code."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args = [ "-F", "src/**/*.kt" ]
}

// We don't want them to be checked too since this is a demo and it's supposed to fail here.
// check.dependsOn "ktlint"